ΠΡΟΓΡΑΜΜΑ Διαλογή_ΛΟΤΤΟ
! ΑΕΠΠ - Οδηγός Μελέτης Μαθητή
! Μη λυμένες ασκήσεις 4.10.17
!
!17. Σε πίνακα ακεραίων ΛΟΤΤΟ[1000000,6] βρίσκονται οι 6 αριθμοί του ΛΟΤΤΟ
!    που έχουν παίξει 1.000.000 παίκτες για το παιχνίδι του ΛΟΤΤΟ και
!    στον πίνακα ΠΑΙΧΤΗΣ[1000000] είναι τα ονόματα των παικτών.
!    Στο πίνακα ΚΕΡΔΙΣΕ[6] είναι οι 6 τυχεροί αριθμοί του ΛΟΤΤΟ που κληρώθηκαν.
!    Να γραφεί πρόγραμμα σε "ΓΛΩΣΣΑ", το οποίο :
!    1) Να γεμίζει τους παραπάνω πίνακες με τιμές από το πληκτρολόγιο.
!    2) Να κάνει την τελική διαλογή- πόσοι παίκτες είχαν 6 επιτυχίες,
!       πόσοι είχαν 5 επιτυχίες, πόσοι είχαν 4 επιτυχίες, πόσοι είχαν 3
!       επιτυχίες, πόσοι είχαν 2 επιτυχίες και πόσοι είχαν 1 επιτυχία -
!       και θα τις αποθηκεύει στον πίνακα ΔΙΑΛΟΓΗ[6].
!    3) Να εμφανίζει τα ονόματα των παικτών που είχαν 6 επιτυχίες.

ΣΤΑΘΕΡΕΣ
  Ν = 100            !1000000
ΜΕΤΑΒΛΗΤΕΣ
  ΑΚΕΡΑΙΕΣ: ΛΟΤΤΟ[Ν, 6], ΚΕΡΔΙΣΕ[6], i, j, k, ΔΙΑΛΟΓΗ[6], σωστοί_αριθμοί
  ΧΑΡΑΚΤΗΡΕΣ: ΠΑΙΧΤΗΣ[Ν] 

ΑΡΧΗ
!   Διάβασμα τιμών
!   Θεωρούμε ότι οι αριθμοί είναι διαφορετικοί μεταξύ τους.

  ΓΡΑΨΕ 'Δώστε τους 6 τυχερούς αριθμούς'
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ 6
    ΔΙΑΒΑΣΕ ΚΕΡΔΙΣΕ[i] 
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ Ν
    ΓΡΑΨΕ 'Δώστε όνομα παίκτη: ', i, ' : '
    ΔΙΑΒΑΣΕ ΠΑΙΧΤΗΣ[i] 
    ΓΡΑΨΕ 'Δώστε τους 6 αριθμούς'
    ΓΙΑ j ΑΠΟ 1 ΜΕΧΡΙ 6
      ΔΙΑΒΑΣΕ ΛΟΤΤΟ[i, j] 
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

! αρχικοποίηση διαλογής
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ 6
    ΔΙΑΛΟΓΗ[i] <- 0
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

! έλεγχος για κερδισμένα δελτία
  ΓΙΑ i ΑΠΟ 1 ΜΕΧΡΙ Ν

    σωστοί_αριθμοί <- 0
    ΓΙΑ j ΑΠΟ 1 ΜΕΧΡΙ 6
      ΓΙΑ k ΑΠΟ 1 ΜΕΧΡΙ 6
        ΑΝ ΛΟΤΤΟ[i, j] = ΚΕΡΔΙΣΕ[k] ΤΟΤΕ
          σωστοί_αριθμοί <- σωστοί_αριθμοί + 1
        ΤΕΛΟΣ_ΑΝ
      ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ
    ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

    ΑΝ σωστοί_αριθμοί > 0 ΤΟΤΕ
      ΔΙΑΛΟΓΗ[σωστοί_αριθμοί] <- ΔΙΑΛΟΓΗ[σωστοί_αριθμοί] + 1
    ΤΕΛΟΣ_ΑΝ

    ΑΝ σωστοί_αριθμοί = 6 ΤΟΤΕ
      ΓΡΑΨΕ 'SUPER τυχερός με 6 επιτυχίες ο : ', ΠΑΙΧΤΗΣ[i] 
    ΤΕΛΟΣ_ΑΝ

  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ

  ΓΙΑ i ΑΠΟ 6 ΜΕΧΡΙ 1 ΜΕ_ΒΗΜΑ -1
    ΓΡΑΨΕ i, ' επιτυχίες :', ΔΙΑΛΟΓΗ[i] 
  ΤΕΛΟΣ_ΕΠΑΝΑΛΗΨΗΣ


ΤΕΛΟΣ_ΠΡΟΓΡΑΜΜΑΤΟΣ